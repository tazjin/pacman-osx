# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=gnupg
pkgver=2.1.7
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('libldap: gpg2keys_ldap'
            'libusb-compat: scdaemon')
depends=('npth' 'libgpg-error' 'libgcrypt' 'libksba' 'libassuan')
source=(ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha1sums=('1a345804f34a2acd05c1555e40ddfa297f38438b' 'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr/local \
		--sysconfdir=/etc \
		--sbindir=/usr/local/bin \
		--libexecdir=/usr/local/lib/gnupg

	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -s gpg2 "${pkgdir}"/usr/local/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/local/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/local/share/man/man1/gpg.1.gz
	rm "${pkgdir}/usr/local/share/gnupg/com-certs.pem" # FS#33059

  # remove unwanted file
  rm -f ${pkgdir}/usr/local/share/info/dir
}
