# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libtasn1
pkgver=4.5
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('texinfo')
source=(http://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('81d272697545e82d39f6bd14854b68f0' 'SKIP')
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') #Nikos Mavrogiannopoulos <nmav@gnutls.org>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr/local \
      --with-packager=Archlinux \
      --with-packager-bug-reports="http://bugs.archlinux.org/" \
      --with-packager-version=${pkgver}-${pkgrel}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # remove unwanted file
  rm -f ${pkgdir}/usr/local/share/info/dir
}
